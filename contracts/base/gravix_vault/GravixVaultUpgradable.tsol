pragma ever-solidity ^0.62.0;


import {GravixVaultHelpers} from "./GravixVaultHelpers.tsol";
import {Callback} from "../../libraries/Callback.tsol";
import {Gas} from "../../libraries/Gas.tsol";
import {Errors} from "../../libraries/Errors.tsol";
import {MsgFlag} from "@broxus/contracts/contracts/libraries/MsgFlag.tsol";
import {IGravixAccount} from "../../interfaces/IGravixAccount.tsol";
import {RPlatform as Platform} from "../../Platform.tsol";


abstract contract GravixVaultUpgradable is GravixVaultHelpers {
    function updateGravixAccountCode(TvmCell code, Callback.CallMeta meta) external onlyManager reserveAndSendGas(meta) {
        gravixAccountCode.code = code;
        gravixAccountCode.version += 1;
        emit GravixAccountCodeUpdate(
            meta.callId,
            gravixAccountCode.version - 1,
            gravixAccountCode.version
        );
    }

    function updateOracleProxyCode(TvmCell code, Callback.CallMeta meta) external onlyManager reserveAndSendGas(meta) {
        oracleProxyCode.code = code;
        oracleProxyCode.version += 1;
        emit OracleProxyCodeUpdate(
            meta.callId,
            oracleProxyCode.version - 1,
            oracleProxyCode.version
        );
    }

    function upgradeGravixAccount(Callback.CallMeta meta) external view reserve {
        require (msg.value >= Gas.GRAVIX_ACC_UPGRADE_VALUE, Errors.LOW_MSG_VALUE);

        _upgradeGravixAccountByUser(msg.sender, 0, meta);
    }

    function forceUpgradeGravixAccountsByUsers(address[] users, Callback.CallMeta meta) external view onlyManager reserve {
        require (msg.value >= Gas.GRAVIX_ACC_UPGRADE_VALUE * (users.length + 1), Errors.LOW_MSG_VALUE);

        this._upgradeGravixAccountsBatch{flag: MsgFlag.ALL_NOT_RESERVED}(users, 0, true,meta);

    }

    function forceUpgradeGravixAccountsByContracts(address[] contracts, Callback.CallMeta meta) external view onlyManager reserve {
        require (msg.value >= Gas.GRAVIX_ACC_UPGRADE_VALUE * (contracts.length + 1), Errors.LOW_MSG_VALUE);

        this._upgradeGravixAccountsBatch{flag: MsgFlag.ALL_NOT_RESERVED}(contracts, 0, false,meta);
    }

    function _upgradeGravixAccountsBatch(address[] addrs, uint128 startIdx, bool userAddresses, Callback.CallMeta meta) external view reserveAndSendGas(meta) {
        require (msg.sender == address(this), Errors.NOT_VAULT);

        uint128 batchSize = 20;
        for (; startIdx < addrs.length && batchSize != 0; startIdx++) {
            batchSize--;
            if (userAddresses) {
                _upgradeGravixAccountByUser(addrs[startIdx], Gas.GRAVIX_ACC_UPGRADE_VALUE, meta);
            } else {
                _upgradeGravixAccountByContract(addrs[startIdx], Gas.GRAVIX_ACC_UPGRADE_VALUE, meta);
            }
        }

        if (startIdx < addrs.length) {
            this._upgradeGravixAccountsBatch{value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false}(
                addrs, startIdx, userAddresses,meta
            );
        }
    }

    function _upgradeGravixAccountByContract(address gravixAcc, uint128 value, Callback.CallMeta meta) internal view {
        uint16 flag = value == 0 ? MsgFlag.ALL_NOT_RESERVED : 1;
        IGravixAccount(gravixAcc).upgrade{ value: value, flag: flag }(
            gravixAccountCode.code,
            gravixAccountCode.version,
            meta
        );
    }

    function _upgradeGravixAccountByUser(address user, uint128 value, Callback.CallMeta meta) internal view {
        address gravixAcc = getGravixAccountAddress(user);
        _upgradeGravixAccountByContract(gravixAcc, value, meta);
    }

    function onGravixAccountUpgrade(
        address user,
        uint32 oldVersion,
        uint32 newVersion,
        Callback.CallMeta meta
    ) external view override onlyGravixAccount(user) reserveAndSuccessCallback(meta) {
        emit GravixAccountUpgrade(meta.callId, user, oldVersion, newVersion);
    }

    function onGravixAccountDeploy(
        address user, Callback.CallMeta meta
    ) external view override onlyGravixAccount(user) reserveAndSuccessCallback(meta) {
        emit GravixAccountDeploy(meta.callId, user);
    }

    function deployGravixAccount(
        address referrer, Callback.CallMeta meta
    ) external view responsible reserveAndSendGas(meta) returns (address account) {
        require (msg.value > Gas.GRAVIX_ACCOUNT_DEPLOY_VALUE, Errors.LOW_MSG_VALUE);
        referrer = referrer == msg.sender ? address(0) : referrer;
        account = _deployGravixAccount(msg.sender, referrer);
    }

    function _deployGravixAccount(address user, address referrer) internal view returns (address) {
        TvmBuilder constructorParams;

        constructorParams.store(gravixAccountCode.version); // 32
        constructorParams.store(gravixAccountCode.version); // 32
        constructorParams.store(referrer); // 267

        return new Platform{
            stateInit: _buildGravixAccountInitData(_buildGravixAccountParams(user)),
            value: Gas.GRAVIX_ACCOUNT_DEPLOY_VALUE
        }(gravixAccountCode.code, constructorParams.toCell(), user);
    }
}
