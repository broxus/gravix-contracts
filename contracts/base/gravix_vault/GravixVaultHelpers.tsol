pragma ever-solidity ^0.62.0;


import "broxus-token-contracts/contracts/interfaces/ITokenRootUpgradeable.tsol";
import "broxus-token-contracts/contracts/interfaces/ITokenWalletUpgradeable.tsol";
import "broxus-token-contracts/contracts/interfaces/IAcceptTokensTransferCallback.tsol";
import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";
import "../../libraries/Gas.tsol";
import "../../libraries/Callback.tsol";
import "../../libraries/PlatformTypes.tsol";
import "../../libraries/Errors.tsol";
import "../../interfaces/ICallbackReceiver.tsol";
import "../../interfaces/IGravixAccount.tsol";
import "./GravixVaultStorage.tsol";
import "../../OracleProxy.tsol";
import {DateTime as DateTimeLib} from "../../libraries/DateTime.tsol";
import {RPlatform as Platform} from "../../Platform.tsol";


abstract contract GravixVaultHelpers is GravixVaultStorage {
    function getDetails() external view responsible returns (
        address _owner,
        address _marketManager,
        address _oracle,
        address _priceNode,
        uint256 _pricePubkey,
        address _usdt,
        address _usdtWallet,
        address _stgUsdt,
        address _stgUsdtWallet,
        address _treasury,
        address _projectFund,
        address _devFund,
        uint128 _poolBalance, // liquidity deposits
        uint128 _stgUsdtSupply, // amount of minted stgUsdt
        uint128 _targetPrice,
        uint128 _insuranceFund, // collected fees, pnl and etc.
        uint128 _insuranceFundLimit,
        uint64[3] _insuranceFundOverflowDistributionSchema,
        uint128 _collateralReserve, // sum of all usdt provided as a collateral for open order
        uint128 _totalNOI,
        bool _totalNOILimitEnabled,
        uint64 _maxPoolUtilRatio,
        uint128 _minPositionCollateral,
        bool _paused,
        uint64 _liquidationThresholdRate,
        uint64 _liquidatorRewardShare,
        uint64[2] _openFeeDistributionSchema,
        uint64[2] _closeFeeDistributionSchema,
        uint32 _marketCount
    ) {
        return { value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS }(
            owner,
            marketManager,
            oracle,
            priceNode,
            pricePubkey,
            usdt,
            usdtWallet,
            stgUsdt,
            stgUsdtWallet,
            treasury,
            projectFund,
            devFund,
            poolBalance, // liquidity deposits
            stgUsdtSupply, // amount of minted stgUsdt
            targetPrice,
            insuranceFund, // collected fees, pnl and etc.
            insuranceFundLimit,
            insuranceFundOverflowDistributionSchema,
            collateralReserve, // sum of all usdt provided as a collateral for open order
            totalNOI,
            totalNOILimitEnabled,
            maxPoolUtilRatio,
            minPositionCollateral,
            paused,
            liquidationThresholdRate,
            liquidatorRewardShare,
            openFeeDistributionSchema,
            closeFeeDistributionSchema,
            marketCount
        );
    }

    function getCodes() external view responsible returns (
        TvmCell _oracleProxyCode,
        TvmCell _platformCode,
        TvmCell _gravixAccountCode,
        uint32 _oracleProxyVersion,
        uint32 _gravixAccountVersion,
        uint32 _gravixVaultVersion
    ) {
        return { value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS }(
            oracleProxyCode,
            platformCode,
            gravixAccountCode,
            oracleProxyVersion,
            gravixAccountVersion,
            gravixVaultVersion
        );
    }

    function _sendCallbackOrGas(address callback_receiver, uint32 nonce, bool success, address send_gas_to) internal pure {
        if (nonce > 0) {
            if (success) {
                ICallbackReceiver(
                    callback_receiver
                ).acceptSuccessCallback{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(nonce);
            } else {
                ICallbackReceiver(
                    callback_receiver
                ).acceptFailCallback{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(nonce);
            }
        } else {
            send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
        }
    }

    function _setupTokenWallets() internal view {
        ITokenRoot(usdt).deployWallet{value: Gas.TOKEN_WALLET_DEPLOY_VALUE, callback: IGravixVault.receiveTokenWalletAddress }(
            address(this), // owner
            Gas.TOKEN_WALLET_DEPLOY_VALUE / 2 // deploy grams
        );
        ITokenRoot(stgUsdt).deployWallet{value: Gas.TOKEN_WALLET_DEPLOY_VALUE, callback: IGravixVault.receiveTokenWalletAddress }(
            address(this), // owner
            Gas.TOKEN_WALLET_DEPLOY_VALUE / 2 // deploy grams
        );
    }

    function _timeToDateTime(uint year, uint month, uint day, Time time) internal pure returns (DateTime dt) {
        return DateTime(uint16(year), uint8(month), uint8(day), time.hour, time.minute);
    }

    function _dateTimeToTimestamp(DateTime dt) internal pure returns (uint32) {
        return uint32(DateTimeLib.timestampFromDateTime(dt.year, dt.month, dt.day, dt.hour, dt.minute, 0));
    }

    function encodeMarketOrderPayload(
        uint32 market_idx,
        PositionType position_type,
        uint32 leverage,
        uint128 expected_price,
        uint64 max_slippage_rate,
        IEthereumEvent.EthereumEventOracleData event_data,
        OffchainPrice price,
        uint32 nonce,
        uint32 call_id
    ) external pure returns (TvmCell payload) {
        TvmCell cell = abi.encode(market_idx, position_type, leverage, expected_price, max_slippage_rate, event_data, price);
        return encodeTokenTransferPayload(Action.MarketOrder, nonce, call_id, cell);
    }

    function decodeMarketOrderPayload(
        TvmCell action_payload
    ) public pure returns (
        uint32 market_idx,
        PositionType position_type,
        uint32 leverage,
        uint128 expected_price,
        uint64 max_slippage_rate,
        IEthereumEvent.EthereumEventOracleData event_data,
        OffchainPrice price
    ) {
        (
            market_idx, position_type, leverage, expected_price, max_slippage_rate, event_data, price
        ) = abi.decode(action_payload, (uint32, PositionType, uint32, uint128, uint64, IEthereumEvent.EthereumEventOracleData, OffchainPrice));
    }
    
    function encodeLiquidityDeposit(uint32 nonce, uint32 call_id) public pure returns (TvmCell payload) {
        TvmCell empty;
        return encodeTokenTransferPayload(Action.LiquidityDeposit, nonce, call_id, empty);
    }

    function encodeLiquidityWithdraw(uint32 nonce, uint32 call_id) public pure returns (TvmCell payload) {
        TvmCell empty;
        return encodeTokenTransferPayload(Action.LiquidityWithdraw, nonce, call_id, empty);
    }

    function encodeInsuranceDeposit(uint32 nonce, uint32 call_id) public pure returns (TvmCell payload) {
        TvmCell empty;
        return encodeTokenTransferPayload(Action.InsuranceDeposit, nonce, call_id, empty);
    }

    function encodeTokenTransferPayload(
        Action action, uint32 nonce, uint32 call_id, TvmCell action_payload
    ) public pure returns (TvmCell payload) {
        return abi.encode(action, nonce, call_id, action_payload);
    }

    function decodeTokenTransferPayload(TvmCell payload) public pure returns (
        Action action, uint32 nonce, uint32 call_id, TvmCell action_payload, bool correct
    ){
        // check if payload assembled correctly
        TvmSlice slice = payload.toSlice();
        // 1 uint8 + 2 uint32 and 1 cell
        (uint16 bits, uint8 refs) = slice.size();
        if ((bits == 8 + 32 + 32) && (refs == 1) && payload.toSlice().decode(uint8) <= uint8(Action.InsuranceDeposit)) {
            (action, nonce, call_id, action_payload) = abi.decode(payload, (Action, uint32, uint32, TvmCell));
            correct = true;
        }
    }

    function checkSign(
        uint128 p, uint32 t1, uint32 t2, string tick, TvmCell signature
    ) public view returns (bool) {
        TvmCell data = abi.encode(p, t1, t2, tick);
        return tvm.checkSign(tvm.hash(data), signature.toSlice(), pricePubkey);
    }

    function checkPrice(OffchainPrice price, PriceNode config) public view returns (bool) {
        if (price.ticker != config.ticker) return false;
        if (now - price.oracleTime > config.maxOracleDelay) return false;
        if (now - price.serverTime > config.maxServerDelay) return false;
        return checkSign(price.price, price.serverTime, price.oracleTime, price.ticker, price.signature);
    }

    function getGravixAccountAddress(address user) public view responsible returns (address) {
        return { value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false } address(
            tvm.hash(_buildGravixAccountInitData(_buildGravixAccountParams(user)))
        );
    }

    function getOracleProxyAddress(uint64 nonce) public view responsible returns (address) {
        return { value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false } address(
            tvm.hash(_buildOracleProxyInitData(nonce))
        );
    }

    function _makeCell(uint32 nonce) internal pure returns (TvmCell) {
        TvmBuilder builder;
        if (nonce > 0) {
            builder.store(nonce);
        }
        return builder.toCell();
    }

    function _transfer(
        address wallet, uint128 amount, address receiver, TvmCell payload, address send_gas_to, uint16 flag
    ) internal pure {
        uint128 value = flag != MsgFlag.ALL_NOT_RESERVED ? Gas.TOKEN_TRANSFER_VALUE : 0;
        bool notify = false;
        // notify = true if payload is non-empty
        TvmSlice slice = payload.toSlice();
        if (slice.bits() > 0 || slice.refs() > 0) {
            notify = true;
        }
        ITokenWallet(wallet).transfer{value: value, flag: flag}(
            amount,
            receiver,
            0,
            send_gas_to,
            notify,
            payload
        );
    }

    function _buildOracleProxyInitData(uint64 nonce) internal view returns (TvmCell) {
        return tvm.buildStateInit({
            contr: OracleProxy,
            varInit: {
                vault: address(this),
                nonce: nonce
            },
            pubkey: 0,
            code: oracleProxyCode
        });
    }

    function _buildGravixAccountParams(address user) internal pure returns (TvmCell) {
        TvmBuilder builder;
        builder.store(user);
        return builder.toCell();
    }

    function _buildGravixAccountInitData(TvmCell _initialData) internal view returns (TvmCell) {
        return tvm.buildStateInit({
            contr: Platform,
            varInit: {
                root: address(this),
                platformType: PlatformTypes.GravixAccount,
                initialData: _initialData,
                platformCode: platformCode
            },
            pubkey: 0,
            code: platformCode
        });
    }

    modifier onlyGravixAccount(address user) {
        address gravix_account_addr = getGravixAccountAddress(user);
        require (msg.sender == gravix_account_addr, Errors.NOT_GRAVIX_ACCOUNT);
        _;
    }

    modifier onlyOracleProxy(uint64 nonce) {
        address proxy = getOracleProxyAddress(nonce);
        require (msg.sender == proxy, Errors.NOT_ORACLE_PROXY);
        _;
    }

    function _reserve() internal pure returns (uint128) {
        return math.max(address(this).balance - msg.value, CONTRACT_MIN_BALANCE);
    }

    modifier reserve() {
        tvm.rawReserve(_reserve(), 0);
        _;
    }

    modifier reserveAndSuccessCallback(Callback.CallMeta meta) {
        tvm.rawReserve(_reserve(), 0);
        _;
        _sendCallbackOrGas(msg.sender, meta.nonce, true, meta.send_gas_to);
    }

    modifier reserveAndFailCallback(Callback.CallMeta meta) {
        tvm.rawReserve(_reserve(), 0);
        _;
        _sendCallbackOrGas(msg.sender, meta.nonce, false, meta.send_gas_to);
    }

    modifier reserveAndSendGas(Callback.CallMeta meta) {
        tvm.rawReserve(_reserve(), 0);
        _;
        meta.send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
    }

    modifier onlyOwner() {
        require(msg.sender == owner, Errors.NOT_OWNER);
        _;
    }

    modifier onlyMarketManager() {
        require (msg.sender == owner || msg.sender == marketManager, Errors.NOT_OWNER);
        _;
    }

    modifier onlyActive() {
        require (!paused || msg.sender == owner, Errors.NOT_ACTIVE);
        _;
    }
}
