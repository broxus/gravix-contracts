pragma ever-solidity ^0.62.0;



import "broxus-token-contracts/contracts/interfaces/ITokenRootUpgradeable.tsol";
import "broxus-token-contracts/contracts/interfaces/ITokenWalletUpgradeable.tsol";
import "broxus-token-contracts/contracts/interfaces/IAcceptTokensTransferCallback.tsol";
import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";
import "../../libraries/Gas.tsol";
import "../../libraries/Callback.tsol";
import "../../interfaces/IGravixAccount.tsol";
import "./GravixVaultLiquidityPool.tsol";
import {DateTime as DateTimeLib} from "../../libraries/DateTime.tsol";


abstract contract GravixVaultMarkets is GravixVaultLiquidityPool {
    function marketOpen(uint32 market_idx) public view returns (bool open) {
        Market _market = markets[market_idx];

        if (_market.paused) return false;
        if (!_market.scheduleEnabled) return true;

        (
            uint year, uint month, uint day,,,
        ) = DateTimeLib.timestampToDateTime(now);
        uint cur_week_day = DateTimeLib.getDayOfWeek(now);

        // first check if market is shut down because of weekend
        // weekend intervals are sorted by beginning and cant intersect
        // so that its enough to check 1st interval with lower start
        optional(uint32, DateTimeInterval) opt = weekends[market_idx].prevOrEq(now);
        if (opt.hasValue()) {
            (,DateTimeInterval last_weekend) = opt.get();
            if (now <= _dateTimeToTimestamp(last_weekend.to)) return false;
        }

        // now lets check if we are on working hours of market
        mapping (uint8 => TimeInterval[]) working_hours = workingHours[market_idx];
        if (!working_hours.exists(uint8(cur_week_day))) return false;
        // ok, so, we are on working day, lets get timestamp of start/end of the working day
        TimeInterval[] work_intervals = working_hours[uint8(cur_week_day)];
        for (TimeInterval interval: work_intervals) {
            uint32 from_ts = _dateTimeToTimestamp(_timeToDateTime(year, month, day, interval.from));
            uint32 to_ts = _dateTimeToTimestamp(_timeToDateTime(year, month, day, interval.to));
            // end not set => work till the end of the day, set infinite timestamp to bypass check
            if (interval.to.hour == 0 && interval.to.minute == 0) to_ts = 2**32 - 1;
            if (now >= from_ts && now <= to_ts) return true;
        }
        return false;
    }

    function addMarkets(
        MarketConfig[] new_markets,
        Callback.CallMeta meta
    ) external onlyOwner reserveAndSuccessCallback(meta) {
        for (MarketConfig _market_config : new_markets) {
            require (validateMarketConfig(_market_config), Errors.BAD_INPUT);

            Market new_market;
            new_market.priceSource = _market_config.priceSource;
            new_market.maxTotalLongsUSD = _market_config.maxLongsUSD;
            new_market.maxTotalShortsUSD = _market_config.maxShortsUSD;
            new_market.noiWeight = _market_config.noiWeight;
            new_market.maxLeverage = _market_config.maxLeverage;
            new_market.depthAsset = _market_config.depthAsset;
            new_market.fees = _market_config.fees;
            new_market.scheduleEnabled = _market_config.scheduleEnabled;

            workingHours[marketCount] = _market_config.workingHours;
            markets[marketCount] = new_market;
            marketCount += 1;

            emit NewMarket(meta.call_id, _market_config);
        }
    }

    function setMarketsConfigs(
        mapping (uint32 => MarketConfig) new_configs, Callback.CallMeta meta
    ) external onlyOwner reserveAndSuccessCallback(meta) {
        for ((uint32 market_idx, MarketConfig config) : new_configs) {
            require (markets.exists(market_idx), Errors.BAD_INPUT);
            require (validateMarketConfig(config), Errors.BAD_INPUT);

            Market market = markets[market_idx];
            market.priceSource = config.priceSource;
            market.maxTotalLongsUSD = config.maxLongsUSD;
            market.maxTotalShortsUSD = config.maxShortsUSD;
            market.noiWeight = config.noiWeight;
            market.maxLeverage = config.maxLeverage;
            market.depthAsset = config.depthAsset;
            market.fees = config.fees;
            market.scheduleEnabled = config.scheduleEnabled;

            workingHours[market_idx] = config.workingHours;
            markets[market_idx] = market;

            emit MarketConfigUpdate(meta.call_id, market_idx, config);
        }
    }

    function setOracleConfigs(
        mapping (uint32 => OracleConfig) new_oracles, Callback.CallMeta meta
    ) external onlyOwner reserveAndSuccessCallback(meta) {
        for ((uint32 market_idx, OracleConfig oracle_conf) : new_oracles) {
            require (markets.exists(market_idx), Errors.BAD_INPUT);

            oracleConfigs[market_idx] = oracle_conf;
            emit MarketOracleConfigUpdate(meta.call_id, market_idx, oracle_conf);
        }
    }

    function validateMarketConfig(MarketConfig config) public pure returns (bool correct) {
        correct = true && config.maxLeverage >= Constants.LEVERAGE_BASE;
        correct = correct && config.fees.fundingBaseRatePerHour < Constants.HUNDRED_PERCENT;
        correct = correct && config.fees.borrowBaseRatePerHour < Constants.HUNDRED_PERCENT;
        correct = correct && config.fees.baseSpreadRate < Constants.HUNDRED_PERCENT;
        correct = correct && config.fees.baseDynamicSpreadRate < Constants.HUNDRED_PERCENT;
        correct = correct && config.fees.closeFeeRate < Constants.HUNDRED_PERCENT;
        correct = correct && config.fees.openFeeRate < Constants.HUNDRED_PERCENT;

        for ((uint8 key, TimeInterval[] intervals) : config.workingHours) {
            correct = correct && key >= DateTimeLib.DOW_MON && key <= DateTimeLib.DOW_SUN;
            for (TimeInterval interval : intervals) {
                correct = correct && validateTimeInterval(interval);
            }
        }
    }

    function setMarketsPause(
        mapping (uint32 => bool) pause, Callback.CallMeta meta
    ) external onlyMarketManager reserveAndSuccessCallback(meta) {
        for ((uint32 market_idx, bool new_state) : pause) {
            require (markets.exists(market_idx), Errors.BAD_INPUT);

            markets[market_idx].paused = new_state;
            emit MarketPause(meta.call_id, market_idx, new_state);
        }
    }

    function setMarketsWorkingHours(
        mapping (uint32 => mapping (uint8 => TimeInterval[])) market_to_working_hours,
        Callback.CallMeta meta
    ) external onlyMarketManager reserveAndSuccessCallback(meta) {
        for ((uint32 market_idx, mapping (uint8 => TimeInterval[]) working_hours) : market_to_working_hours) {
            _setMarketWorkingHours(market_idx, working_hours, meta);
        }
    }

    function setMarketsCommonWorkingHours(
        uint32[] market_idx,
        mapping (uint8 => TimeInterval[]) working_hours,
        Callback.CallMeta meta
    ) external onlyMarketManager reserveAndSuccessCallback(meta) {
        for (uint i = 0; i < market_idx.length; i++) {
            _setMarketWorkingHours(market_idx[i], working_hours, meta);
        }
    }

    function _setMarketWorkingHours(
        uint32 market_idx,
        mapping (uint8 => TimeInterval[]) working_hours,
        Callback.CallMeta meta
    ) internal {
        require (markets.exists(market_idx), Errors.BAD_INPUT);

        for ((uint8 key, TimeInterval[] intervals) : working_hours) {
            require (key >= DateTimeLib.DOW_MON && key <= DateTimeLib.DOW_SUN, Errors.BAD_INPUT);
            for (TimeInterval interval : intervals) {
                require (validateTimeInterval(interval), Errors.BAD_INPUT);
            }
        }

        workingHours[market_idx] = working_hours;
        emit MarketScheduleUpdate(meta.call_id, market_idx, working_hours);
    }

    function addMarketsWeekends(
        uint32[] market_idx,
        DateTimeInterval[] new_weekends,
        Callback.CallMeta meta
    ) external onlyMarketManager reserveAndSuccessCallback(meta) {
        require (market_idx.length == new_weekends.length, Errors.BAD_INPUT);

        for (uint i = 0; i < market_idx.length; i++) {
            _addMarketWeekends(market_idx[i], new_weekends[i], meta);
        }
    }

    function addMarketsCommonWeekends(
        uint32[] market_idx,
        DateTimeInterval new_weekends,
        Callback.CallMeta meta
    ) external onlyMarketManager reserveAndSuccessCallback(meta) {
        for (uint i = 0; i < market_idx.length; i++) {
            _addMarketWeekends(market_idx[i], new_weekends, meta);
        }
    }

    function _addMarketWeekends(
        uint32 market_idx,
        DateTimeInterval _new_weekend,
        Callback.CallMeta meta
    ) internal {
        require (markets.exists(market_idx), Errors.BAD_INPUT);
        require (validateDateTimeInterval(_new_weekend));

        uint32 _new_weekend_start = _dateTimeToTimestamp(_new_weekend.from);
        uint32 _new_weekend_end = _dateTimeToTimestamp(_new_weekend.to);

        // check that previous weekend end before start of this weekend
        optional(uint32, DateTimeInterval) _opt = weekends[market_idx].prevOrEq(_new_weekend_start);
        if (_opt.hasValue()) {
            (, DateTimeInterval _prev_weekend) = _opt.get();
            uint32 _prev_weekend_end = _dateTimeToTimestamp(_prev_weekend.to);

            require (_new_weekend_start >= _prev_weekend_end, Errors.BAD_INPUT);
        }

        // check that next weekend start after this weekend
        optional(uint32, DateTimeInterval) _opt2 = weekends[market_idx].next(_new_weekend_start);
        if (_opt2.hasValue()) {
            (uint32 _next_weekend_start,) = _opt2.get();

            require (_next_weekend_start >= _new_weekend_end, Errors.BAD_INPUT);
        }

        weekends[market_idx][_new_weekend_start] = _new_weekend;
        emit MarketWeekends(meta.call_id, market_idx, _new_weekend);
    }
    
    function clearMarketsWeekends(
        uint32[] market_idx, Callback.CallMeta meta
    ) external onlyMarketManager reserveAndSuccessCallback(meta) {
        for (uint i = 0; i < market_idx.length; i++) {
            delete weekends[market_idx[i]];
            emit MarketWeekendsCleared(meta.call_id, market_idx[i]);
        }
    }

    function validateTimeInterval(TimeInterval val) public pure returns (bool correct) {
        return val.to.hour > val.from.hour ||
            (val.to.hour == val.from.hour && val.to.minute > val.from.minute) ||
            (val.to.hour == 0 && val.to.minute == 0);
    }

    function validateDateTimeInterval(DateTimeInterval val) public pure returns (bool correct) {
        return _dateTimeToTimestamp(val.to) > _dateTimeToTimestamp(val.from);
    }

    function getMarketWorkingHours(uint32 market_idx) external view responsible returns (mapping (uint8 => TimeInterval[])) {
        return { value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS }workingHours[market_idx];
    }

    function getMarketWeekends(uint32 market_idx) external view responsible returns (mapping (uint32 => DateTimeInterval)) {
        return { value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS }weekends[market_idx];
    }

    function getMarket(uint32 market_idx) external view responsible returns (Market _market) {
        return { value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS } markets[market_idx];
    }

    function getMarkets() external view responsible returns (mapping (uint32 => Market) _markets) {
        return { value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS } markets;
    }

    function getOracleConfigs() external view responsible returns (mapping (uint32 => OracleConfig) _oracle_configs) {
        return { value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS } oracleConfigs;
    }

    function getOracleConfig(uint32 market_idx) external view responsible returns (OracleConfig _oracle) {
        return { value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS } oracleConfigs[market_idx];
    }
}
