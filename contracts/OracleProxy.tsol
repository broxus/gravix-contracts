pragma ever-solidity ^0.62.0;


import "./interfaces/IOnRateCallback.tsol";
import "./interfaces/IGravixVault.tsol";
import "./interfaces/ITWAPOracle.tsol";
import "./libraries/Callback.tsol";
import "./libraries/Errors.tsol";
import "./interfaces/INonTokenOraclePriceCallback.tsol";
import "./interfaces/IOnNonTokenPriceReceived.tsol";
import "./interfaces/IPriceNode.tsol";
import "./interfaces/IEthereumEvent.tsol";
import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";
import "./interfaces/IOnPriceCallback.tsol";


contract OracleProxy is IOnRateCallback, IOnNonTokenPriceReceived, IOnPriceCallback {
    address static vault;
    uint64 static nonce;

    // this always set
    address usdt;
    address oracle;
    address price_node;
    uint32 market_idx;
    IGravixVault.OracleType price_source;
    IGravixVault.OracleConfig oracle_config;
    IEthereumEvent.EthereumEventOracleData event_data;
    Callback.CallMeta meta;

    // this is set for open/close
    uint32 position_key;
    address user;
    address referrer;

    // this is set for open
    uint128 collateral;
    uint32 leverage;
    IGravixVault.PositionType position_type;

    // this is set for liquidations
    IGravixVault.PositionIdx[] positions;
    address liquidator;

    enum CallbackType { Execute, Close, Liquidation }
    CallbackType callbackType;
    // dex oracle utility staff
    // pair addr => current reserves
    mapping (address => uint128[]) pair_reserves;

    uint128 constant DEX_ORACLE_REQUEST_VALUE = 0.1 ever;
    uint128 constant SCALING_FACTOR = 10**18;
    uint128 constant CONTRACT_MIN_BALANCE = 0.01 ever;

    constructor (
        address _usdt,
        address _price_node,
        address _oracle,
        uint32 _market_idx,
        IGravixVault.OracleType _price_source,
        IGravixVault.OracleConfig _oracle_config,
        IEthereumEvent.EthereumEventOracleData _event_data,
        Callback.CallMeta _meta
    ) public {
        require (msg.sender == vault, Errors.BAD_SENDER);

        usdt = _usdt;
        oracle = _oracle;
        price_node = _price_node;
        market_idx = _market_idx;
        price_source = _price_source;
        oracle_config = _oracle_config;
        event_data = _event_data;
        meta = _meta;
    }

    function setExecuteCallback(
        address _user,
        uint32 _position_key,
        uint128 _collateral,
        uint32 _leverage,
        IGravixVault.PositionType _position_type
    ) external {
        require (msg.sender == vault, Errors.BAD_SENDER);

        user = _user;
        position_key = _position_key;
        collateral = _collateral;
        leverage = _leverage;
        position_type = _position_type;
        callbackType = CallbackType.Execute;
        _collectPrice(user);
    }

    function setCloseCallback(address _user, uint32 _position_key, address _referrer) external {
        require (msg.sender == vault, Errors.BAD_SENDER);

        user = _user;
        position_key = _position_key;
        referrer = _referrer;
        callbackType = CallbackType.Close;
        _collectPrice(user);
    }

    function setLiquidationCallback(address _liquidator, IGravixVault.PositionIdx[] _positions) external {
        require (msg.sender == vault, Errors.BAD_SENDER);

        liquidator = _liquidator;
        positions = _positions;
        callbackType = CallbackType.Liquidation;
        _collectPrice(liquidator);
    }

    function _collectPrice(address send_gas_to) internal view {
        if (price_source == IGravixVault.OracleType.ChainlinkProxy) {
            _collectPriceFromChainlink(send_gas_to);
        } else if (price_source == IGravixVault.OracleType.Dex) {
            _collectPriceFromDex();
        } else if (price_source == IGravixVault.OracleType.PriceNode) {
            _collectPriceFromPriceNode();
        }
    }

    function _collectPriceFromDex() internal view {
        IGravixVault.DexOracle dex = oracle_config.dex;

        for (uint i = 0; i < dex.path.length; i++) {
            IGravixVault.Pair pair = dex.path[i];
            TvmCell request_payload = abi.encode(i);

            ITWAPOracle(pair.addr).rate{value: DEX_ORACLE_REQUEST_VALUE}(
                uint32(now) - 1, uint32(now), address(this), request_payload
            );
        }
    }

    function _collectPriceFromChainlink(address send_gas_to) internal view {
        tvm.rawReserve(CONTRACT_MIN_BALANCE, 0);
        TvmCell empty; // we dont need any special payload, all data is here
        INonTokenOraclePriceCallback(oracle).makeRequest{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
            event_data, empty, oracle_config.chainlink.ticker, send_gas_to
        );
    }

    function _collectPriceFromPriceNode() internal view {
        tvm.rawReserve(CONTRACT_MIN_BALANCE, 0);

        IPriceNode(price_node).makeRequest{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(oracle_config.priceNode.ticker);
    }

    function _sendCallback(uint128 price) internal view {
        tvm.rawReserve(CONTRACT_MIN_BALANCE, 0);
        if (callbackType == CallbackType.Execute) {
            IGravixVault(vault).oracle_executeMarketOrder{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
                nonce,
                user,
                position_key,
                market_idx,
                collateral,
                leverage,
                position_type,
                price,
                meta
            );
        } else if (callbackType == CallbackType.Close) {
            IGravixVault(vault).oracle_closePosition{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
                nonce,
                user,
                position_key,
                market_idx,
                price,
                referrer,
                meta
            );
        } else {
            IGravixVault(vault).oracle_liquidatePositions{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
                nonce,
                liquidator,
                market_idx,
                positions,
                price,
                meta
            );
        }
    }

    function onPriceCallback(uint128 price, string ticker) external override {
        // ttl is checked on price node side
        require (msg.sender == price_node, Errors.BAD_SENDER);
        // just in case
        require (ticker == oracle_config.priceNode.ticker, Errors.BAD_INPUT);

        _sendCallback(price);
    }

    function onPriceReceived(int256 price, uint256 timestamp, uint256, TvmCell) external override {
        require (msg.sender == oracle, Errors.BAD_SENDER);
        // too old event
        if ((now - oracle_config.chainlink.ttl) > timestamp) {
            _reject();
        } else {
            _sendCallback(uint128(price));
        }
    }

    function onPriceRejected(TvmCell) external override {
        require (msg.sender == oracle, Errors.BAD_SENDER);
        _reject();
    }

    function _reject() internal view {
        if (callbackType == CallbackType.Liquidation) {
            liquidator.transfer(0, false, MsgFlag.ALL_NOT_RESERVED + MsgFlag.DESTROY_IF_ZERO);
        } else {
            user.transfer(0, false, MsgFlag.ALL_NOT_RESERVED + MsgFlag.DESTROY_IF_ZERO);
        }
    }

    // dex oracle callback
    function onRateCallback(
        optional(ITWAPOracle.Rate),
        uint128[] _reserves,
        address _callbackRequester,
        TvmCell _payload
    ) external override {
        require (_callbackRequester == address(this), Errors.BAD_SENDER);

        uint idx = abi.decode(_payload, (uint));
        IGravixVault.DexOracle dex = oracle_config.dex;
        require (msg.sender == dex.path[idx].addr, Errors.BAD_SENDER);

        pair_reserves[msg.sender] = _reserves;

        if (pair_reserves.keys().length == dex.path.length) {
            // ok, we got all reserves we need
            address target_token = dex.targetToken;
            uint128 price = SCALING_FACTOR; // 1 * 10**18

            for (IGravixVault.Pair pair : dex.path) {
                uint128 pair_price;
                uint128[] reserves = pair_reserves[pair.addr];

                if (pair.leftRoot == target_token) {
                    pair_price = math.muldiv(reserves[1], SCALING_FACTOR, reserves[0]);
                    target_token = pair.rightRoot;
                } else if (pair.rightRoot == target_token) {
                    pair_price = math.muldiv(reserves[0], SCALING_FACTOR, reserves[1]);
                    target_token = pair.leftRoot;
                } else {
                    revert (Errors.BAD_DEX_ORACLE_PATH);
                }
                price = math.muldiv(price, pair_price, SCALING_FACTOR);
            }
            // path should resolve in USDT
            // TODO: uncomment for prod
            require (target_token == usdt, Errors.BAD_DEX_ORACLE_PATH);
            // convert to final price using standard token decimals
            price = math.muldiv(price, 10**9, SCALING_FACTOR);
            _sendCallback(price * 100); // add 2 decimals to get 8
        }
    }
}
